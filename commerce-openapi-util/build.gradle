import com.liferay.gradle.plugins.source.formatter.FormatSourceTask;
import com.liferay.gradle.util.GradleUtil

task deploy(overwrite: true)
task formatSourceModule
task generateRestModule(type: JavaExec)
task runClass(type: JavaExec)

formatSourceModule {
	dependsOn tasks.matching { Task task -> task.name.startsWith("formatSourceRestModule")}
}

File[] configFiles = new File("src/main/resources/com/liferay/commerce/openapi/util/config").listFiles()

configFiles.each { File file ->
	Properties properties = new Properties()

	file.withInputStream {
		properties.load(it)
	}

	if (!"true".equals(properties.get("osgi.module.generator.enabled"))) {
		return
	}

	String osgiModuleName = properties.get("osgi.module.name")
	String osgiModuleRootPath = properties.get("osgi.module.root.path")

	task "formatSourceModule_${osgiModuleName}"(type: FormatSourceTask) {
		baseDirName = osgiModuleRootPath + "/" + osgiModuleName
	}

	formatSourceModule.dependsOn "formatSourceModule_${osgiModuleName}"
}

defaultTasks "generateRestModule"

dependencies {
	compile group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.9.8"
	compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.9.8"
	compile group: "com.fasterxml.jackson.dataformat", name: "jackson-dataformat-yaml", version: "2.9.8"
	compile group: "com.liferay", name: "com.liferay.petra.json.web.service.client", version: "11.0.0"
	compile group: "javax.ws.rs", name: "javax.ws.rs-api", version: "2.1"
	compile group: "org.apache.httpcomponents", name: "httpasyncclient", version: "4.1.3"
	compile group: "org.apache.httpcomponents", name: "httpclient", version: "4.5.3"
	compile group: "org.apache.httpcomponents", name: "httpcore", version: "4.4.6"
	compile group: "org.apache.httpcomponents", name: "httpcore-nio", version: "4.4.6"
	compile group: "org.slf4j", name: "slf4j-api", version: "1.7.2"
	compile group: "org.slf4j", name: "slf4j-log4j12", version: "1.7.2"

	compileOnly group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.9.8"

	testCompile group: "junit", name: "junit", version: "4.12"
}

deploy {
	doLast {
		println('Use `generateRestModule` task for generating the OpenAPI module.')
	}
}

generateRestModule {
	finalizedBy formatSourceModule

	classpath = sourceSets.main.runtimeClasspath
	main = "com.liferay.commerce.openapi.util.generator.OSGiRESTModuleGenerator"

	def extensionContainer = project.getExtensions()

	def extraPropertiesExtension = extensionContainer.getExtraProperties()

	def projectProperties = extraPropertiesExtension.properties.entrySet()

	List<String> mainClassArgs = new ArrayList<>();

	for (int i; i < projectProperties.size(); i++) {
		Map.Entry property = projectProperties.getAt(i);

		String key = property.getKey()

		if (!key.startsWith("osgi.module")) {
			continue;
		}

		mainClassArgs.add(key + "=" + GradleUtil.getProperty(project, key, ""));
	}

	args mainClassArgs

	jvmArgs "-Dlog4j.configuration=log4j.xml"
}

runClass {
	classpath = sourceSets.main.runtimeClasspath
	main = GradleUtil.getProperty(project, "mainClass", "")

	jvmArgs "-Dlog4j.configuration=log4j.xml"
}