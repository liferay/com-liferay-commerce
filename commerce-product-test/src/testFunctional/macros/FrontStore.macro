definition {
	macro selectAssociatedFSOptionsValue {
		Click(locator1 = "CommerceFrontStore#FS_PRODUCT_OPTIONS_SELECT");

		Click(
			locator1 = "CommerceFrontStore#FS_PRODUCT_OPTION",
			key_optionValue = "${FSOptionValue}"
		);
	}

	macro viewProductDetail {
		AssertElementPresent(
			locator1 = "CommerceFrontStore#FS_PRODUCT_NAME",
			key_FSProductName = "${FSProductName}"
		);

		if (IsElementPresent(locator1 = "CommerceFrontStore#FS_PRODUCT_OPTIONS_SELECT")) {
			for (var FSOptionValues : list "${FSOptionValuesList}") {
				var FSOptionValue = StringUtil.extractFirst("${FSOptionValues}", ":");
				var FSPrice = StringUtil.extractLast("${FSOptionValues}", ":");
				var FSOptionValueUppercase = StringUtil.upperCase("${FSOptionValue}");

				FrontStore.selectAssociatedFSOptionsValue(FSOptionValue = "${FSOptionValue}");

				echo("Checking ${FSOptionValue} ${FSProductName} SKU is correct...");

				AssertElementPresent(
					locator1 = "CommerceFrontStore#FS_PRODUCT_SKU",
					key_FSProductSKU = "${FSOptionValueUppercase}SKU"
				);

				echo("Checking ${FSOptionValue} ${FSProductName} price is correct...");

				AssertElementPresent(
					locator1 = "CommerceFrontStore#FS_PRODUCT_PRICE",
					key_FSProductPrice = "${FSPrice}"
				);

				echo("Checking ${FSOptionValue} ${FSProductName} image is correct...");

				AssertElementPresent(
					locator1 = "CommerceFrontStore#FS_PRODUCT_FULL_IMAGE",
					value1 = "${FSOptionValue}",
					key_FSProductImage = "${FSOptionValue}"
				);

				echo("Checking ${FSOptionValue} ${FSProductName} thumbnail is correct...");

				AssertElementPresent(
					locator1 = "CommerceFrontStore#FS_PRODUCT_IMAGE_THUMBNAIL",
					value1 = "${FSOptionValue}",
					key_FSProductImageThumb = "${FSOptionValue}"
				);
			}
		}
	}
}